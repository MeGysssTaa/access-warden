/*
 * Copyright 2021 German Vekhorev (DarksideCode)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group 'me.darksidecode.accesswarden.core'
version '1.0.0'
def buildTimestamp = new Date().format('yyyy/MM/dd_HH-mm-ss')

java {
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url 'https://archiva.reflex.rip/repository/public/'

            credentials {
                // ~/.gradle/gradle.properties
                username reflexPrivateUsername
                password reflexPrivatePassword
            }

            authentication {
                basic BasicAuthentication
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = 'access-warden-core'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Access Warden Core'
                description = 'A powerful toolkit for enforcing access restrictions in Java'
                url = 'https://github.com/MeGysssTaa/access-warden'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'DarksideCode'
                        name = 'German Vekhorev'
                    }
                }
            }
        }
    }
}

application {
    mainClassName = 'me.darksidecode.accesswarden.core.AccessWardenCore'
}

jar {
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    manifest {
        attributes 'Main-Class': "${application.mainClassName}"
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/build.properties").withWriter { w ->
            Properties p = new Properties()

            p['version'] = project.version.toString()
            p['timestamp'] = buildTimestamp.toString()

            p.store w, null
        }
    }
}

classes.dependsOn createProperties

def asmVersion = '9.1'
def log4jVersion = '2.14.1'

repositories {
    mavenCentral()

    maven {
        name 'Public Reflex Repository'
        url 'https://archiva.reflex.rip/repository/public/'
    }
}

dependencies {
    implementation project(':access-warden-api')

    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${log4jVersion}"
    implementation group: 'org.ow2.asm', name: 'asm', version: "${asmVersion}"
    implementation group: 'org.ow2.asm', name: 'asm-util', version: "${asmVersion}"
}
